/**
 * Copyright (C) 2013 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package org.nyc.cir.ICE_v1_0_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import gov.nyc.health.ice.service.DiseaseImmunity
import gov.nyc.health.ice.service.DoseStatus
import gov.nyc.health.ice.service.ICELogicHelper
import gov.nyc.health.ice.service.SeriesRules
import gov.nyc.health.ice.service.SupportedDiseaseConcept
import gov.nyc.health.ice.service.SupportedEvaluationConcept
import gov.nyc.health.ice.service.SupportedVaccineConcept
import gov.nyc.health.ice.service.SupportedVaccineGroupConcept
import gov.nyc.health.ice.service.TargetDose
import gov.nyc.health.ice.service.TargetSeries
import gov.nyc.health.ice.service.TimePeriod
import gov.nyc.health.ice.service.TimePeriod.DurationType
import gov.nyc.health.ice.service.Vaccine

expander org.nyc.cir^ICE^1.0.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


// If a shot of PPSV is given below the series absolute minimum age (65 yrs minus 4 days), then evaluation = accepted, reason = Below Recommended Age Series. 
// Shots that are accepted do not count toward the series.
rule "Evaluate PPSV Shot as Accepted if given before absolute minimum age of first dose; does not count towards series completion" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	activation-group "doseAgeCheck"
	salience 1500
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the dose number in the series is == 1
			- make note of the Date this Shot was Administered as $assign_administrationDate
		There is a Series $targetSeries that contains the shot $currentShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PPSV
			- make note of the required Absolute Minimum Age for Dose 1 in this Series as $assign_strTimePeriod
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_administrationDate < $assign_strTimePeriod
	then
		Mark the Dose $currentShot as Accepted for this Series due to "Below Recommended Age"
		Mark that the shot $currentShot does not count towards completion of the series
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// FOR CHILDREN (<19), absolute minimum interval is 52 days between PPSV and PCV (regardless of order given); if given below 52 days, 
// invalid reason= BELOW_MIN_INTERVAL_PCV_PPSV
rule "PPSV: Evaluate PPSV Shot as Invalid if patient <19yrs at time of first shot, absolute minimum interval <52days && > 0days between PPSV and PCV not met" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	salience 1500
	when
		There is an administered shot $currentShot that needs to be evaluated
			- make note of the date this Shot was Administered as $administrationPPSVDate
		There is an administered shot $pcvShot distinct from $currentShot
			- make note of the date this Shot was Administered as $administrationPCVDate
			- the administration date of the shot is < $administrationPPSVDate
		There is a Series $targetSeries that contains the shot $currentShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PPSV
		There is a Series $pcvSeries that contains the shot $pcvShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		// Confirm elapsed time between $dtBirthDate and evalTime < "19y" AND elapsed time between $administrationPPSVDate and $administrationPCVDate < "52d"
		Confirm elapsed time between $dtBirthDate and $administrationPCVDate < "19y" AND elapsed time between $administrationPPSVDate and $administrationPCVDate < "52d"
		
	then
		Mark the Dose $currentShot as Invalid for this Series due to "Below Minimum Interval (PCV PPSV)"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// FOR ADULTS (>= 19) absolute minimum interval is 52 days between PCV and PPSV (in that order), invalid reason= BELOW_MIN_INTERVAL_PCV_PPSV 
 rule "PPSV: Evaluate PPSV Shot as Invalid if patient >=19yrs and <absolute minimum interval 52d && > 0days between PCV & PPSV in that order" ruleflow-group "HistoryEvaluation"
 	dialect "mvel"
 	salience 1500
 	when
 		There is an administered shot $ppsvShot that needs to be evaluated
			- make note of the date this Shot was Administered as $administrationPPSVDate
		There is an administered shot $pcvShot distinct from $ppsvShot
			- make note of the date this Shot was Administered as $administrationPCVDate
			- the administration date of the Shot is < $administrationPPSVDate 				
		There is a Series $targetSeries that contains the shot $ppsvShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PPSV
		There is a Series $pcvSeries that contains the shot $pcvShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		// Confirm elapsed time between $dtBirthDate and evalTime >= "19y" AND elapsed time between $administrationPCVDate and $administrationPPSVDate < "52d"
		Confirm elapsed time between $dtBirthDate and $administrationPCVDate >= "19y" AND elapsed time between $administrationPCVDate and $administrationPPSVDate < "52d"
 	then
		Mark the Dose $ppsvShot as Invalid for this Series due to "Below Minimum Interval (PCV PPSV)"
		Record that this dose rule was processed for the TargetDose $ppsvShot
		Log that this dose rule fired for the dose $ppsvShot in the Series $targetSeries
end
